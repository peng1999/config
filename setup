#!/usr/bin/fish
#make -f install.mk $argv
function check
    echo $argv
    eval $argv
    set s $status
    if not test $s = 0
	echo not 0
	exit $s
    end
end

function ensure_dir
    set dir $argv[1]
    if not test -e $dir
        echo "$dir don't exist, creat it"
        mkdir -p $dir
    end
end

function try_link
    set src $argv[1]
    set dist $argv[2]
    if test -e $dist
        echo $dist already exists, skiped
    else
        ensure_dir (dirname $dist)
        echo link $dist to $src...
        ln -rs $src $dist
    end
end

function vimrc
    ensure_dir ~/tmp/vimundo
    ensure_dir ~/tmp/vimbackup
    try_link $PWD/vimrc/vimrc ~/.vim/vimrc
end

function vundle
    set vpath ~/.vim/bundle/Vundle.vim
    if test -e $vpath
        echo $vpath already exists, skiped
    else
        echo Install Vundle...
        git clone https://github.com/VundleVim/Vundle.vim.git ~/.vim/bundle/Vundle.vim
    end
end

function vim_plugin
    vimrc
    vundle
    try_link $PWD/vimrc/plugin.vim ~/.vim/plugin.vim
    echo Install Vim plugins...
    vim -c 'execute "PluginInstall" | qa'
end

function oh_my_zsh
    set zpath ~/.oh-my-zsh/
    if test -e $zpath
        echo $zpath exists, skiped
    else
        echo install Oh My Zsh...
        curl -fsSL https://raw.githubusercontent.com/robbyrussell/oh-my-zsh/master/tools/install.sh | sh
    end
end

function zshrc
    oh_my_zsh
    rm ~/.zshrc
    set synhi ~/.oh-my-zsh/custom/plugins/zsh-syntax-highlighting
    test ! -d $synhi; and check zsh -c '\'git clone -q https://github.com/zsh-users/zsh-syntax-highlighting.git ${ZSH_CUSTOM:-~/.oh-my-zsh/custom}/plugins/zsh-syntax-highlighting\''
    check ln -rs $PWD/zshrc ~/.zshrc
end

function config.fish
    try_link $PWD/config.fish ~/.config/fish/config.fish
end

function fisherman
    config.fish
    if type -q fisher
        echo command fisher exists, skiped
    else
        echo getting fisherman...
        curl -Lo ~/.config/fish/functions/fisher.fish --create-dirs https://git.io/fisher
    end
end

function git_config
    git config credential.helper cache
end

for cmd in $argv
    switch $cmd
        case vimrc
            vimrc
        case vundle
            vundle
        case vim_plugin
            vim_plugin
        case oh_my_zsh
            oh_my_zsh
        case zshrc
            zshrc
        case config.fish
            config.fish
        case fisherman
            fisherman
        case git_config
            git_config
        case '*'
            echo "The command doesn't exist"
            exit 1
    end
end
